# Maintainer: Inkane <neoinkaneglade@aol.com>
# Contributer: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr)
# Contributer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Tuxbubling <tuxbubling@jabber.fr>
pkgname=cdrtools
pkgver=3.01a07
pkgrel=1
pkgdesc="Original cdrtools supporting CD, DVD and BluRay burning"
arch=('i686' 'x86_64')
url="http://cdrecord.berlios.de/private/cdrecord.html"
license=('CDDL')
depends=('acl')
conflicts=('cdrkit')
provides=('cdrkit')
options=(!makeflags)
backup=('etc/default/cdrecord' 'etc/default/rscsi')
install=cdrtools.install
source=("ftp://ftp.berlios.de/pub/cdrecord/alpha/cdrtools-$pkgver.tar.gz")
md5sums=('b690d11ee6c9464b89d8684172807b51')

build() {
  cd "$srcdir"/cdrtools-${pkgver%a*}
  sed -i 's|/opt/schily|/usr|g' DEFAULTS/Defaults.linux
  sed -i 's|DEFINSGRP=.*|DEFINSGRP=root|' DEFAULTS/Defaults.linux
  
  export GMAKE_NOWARN=true
  make INS_BASE=/usr INS_RBASE=/
}

package() {
  cd "$srcdir"/cdrtools-${pkgver%a*}
  export GMAKE_NOWARN=true

  make INS_BASE=/usr INS_RBASE=/ DESTDIR="$pkgdir" install
  rm -rf "$pkgdir"/usr/include
  rm -rf "$pkgdir"/usr/lib/{*.a,profiled}
  find "$pkgdir" -type d -perm 775 -exec chmod 755 '{}' ';'
  find "$pkgdir" -type f -perm 4711 -exec chmod 755 '{}' ';'
}

# vim:set ts=2 sw=2 et:
