# Maintainer: Inkane <Neoinkaneglade@aol.com>
# Contributer: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr)
# Contributer: Pierre Schmitz <pierre@archlinux.de>

pkgname=grml-zsh-config
pkgver=0.5.5
pkgrel=1
pkgdesc="grml's zsh setup"
arch=('any')
url='http://grml.org/zsh/'
license=('GPL2')
provides=('grmlzshrc')
depends=('zsh')
makedepends=('txt2tags')
source=("http://deb.grml.org/pool/main/g/grml-etc-core/grml-etc-core_${pkgver}.tar.gz")
sha256sums=('8c55883ad27f67ae25c86dba175b8917a2c0345520fb03ae941b39eacd365d04')

build() {
	cd ${srcdir}/grml-etc-core/doc
	make
}

package() {
	cd ${srcdir}/grml-etc-core
	install -D -m644 etc/skel/.zshrc ${pkgdir}/etc/skel/.zshrc
	install -D -m644 etc/zsh/keephack ${pkgdir}/etc/zsh/keephack
	install -D -m644 etc/zsh/zshrc ${pkgdir}/etc/zsh/zshrc

	install -D -m644 doc/grmlzshrc.5 ${pkgdir}/usr/share/man/man5/grmlzshrc.5
	ln -sf grmlzshrc.5.gz ${pkgdir}/usr/share/man/man5/grml-zsh-config.5.gz
}
