# Maintainer: Samir Benmendil <samir.benmendil[at]gmail[dot]com>
# Contributer: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr)
# Contributer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: jimmy-6 <jakub.jarozek {at} gmail.com>
# Contributor: Babets http://aur.archlinux.org/account.php?Action=AccountInfo&ID=6996
 
pkgname=trine
pkgver=1.0.8_update4
pkgrel=2
pkgdesc="[Humble Bundle FrozenByte, Frozen Synapse] A physics-based fantasy action game with three playable characters and co-op mode"
url="http://trine-thegame.com"
license=('custom: "commercial"')
arch=('i686' 'x86_64')
 [ "$CARCH" = "x86_64" ] && _arch='64'
 [ "$CARCH" = "i686" ]   && _arch='32'
depends=('libglade' 'nvidia-cg-toolkit' 'openal' 'sdl_image' 'sdl_ttf' 'libvorbis' 'libx86' 'libxmu' 'libpng12')
makedepends=('unzip')
source=('trine.sh' 'trine-launcher.sh' 'trine.desktop')
install=trine.install
sha512sums=('843629d13ae5ec00e0e195a126fb28783001d59cc81e07bfb636b5136e5892fedd38d3f1e0f0b3b0c8050c4f7b0a74e47e6aaa8f54ccb8e84333f5f3e6048648'
            '5ffc9d563a39e6c9c89845eea4ebacaea678d2912c94823f88db8a78f86cea97a291caec3b2fd1c478a6b66fa221b43110995b83af5c6e71d22d6c3a6f4f797d'
            '0b1c010c27e89837ef23af33161d4e2e91ee64efd20ff6196bf5b25c87c48a60ba0f44a2273a3f1170120247e88178cf72436f547a1bd403d370ff6ba786f6cb')
PKGEXT=".pkg.tar"
 
_archive="TrineUpdate4.${_arch}.run"
 [ "$CARCH" = "x86_64" ] && _archive_md5='9ca9a755f3586aeda47d650808129948'
 [ "$CARCH" = "i686" ]   && _archive_md5='7c84061f9f74052a45d15c35df18d143'
 
_bundle=('humblebundlefrozen' 'humblebundlesynapse')
 
_humble() {
  if [ ! -f "${_archive}" ]; then
    for bundle in ${_bundle[@]}; do
      case ${bundle} in
        "humblebundle"*) _key="_humblebundle${bundle:12}key";;
        *) continue;;
      esac
      if [ -n "${!_key}" ]; then
        echo "Getting your unique ${pkgname} download location."
        _uri="$(curl -s "http://www.humblebundle.com/downloads?key=${!_key}" | grep "${_archive}" | cut -d "'" -f 10)"
        wget $_uri
        mv ${_archive}* ${_archive}
        break
      else
        echo "\"${_key}\" environment variable required for download handling not found."
      fi
    done
    if [ -z "${!_key}" -a ! -f "${startdir}/${_archive}" ]; then
      echo "Unable to download \"${_archive}\"."
      exit 1
    fi
  fi
  if ! echo "${_archive_md5}  ${_archive}" | md5sum -c --quiet; then
    echo "Invalid checksum for ${_archive}"
    return 1
  fi
  unset _key _uri _archive_md5
}
 
build() {
  cd "${srcdir}"
  _humble
 
  unzip -o "${_archive}" -d "${pkgname}" || true
  unset _archive _archive_md5 _uri
}
 
package() {
  cd "${srcdir}"
 
  install -dm755 "${pkgdir}/opt/${pkgname}"
  cp -dpr --no-preserve=ownership "${pkgname}" "${pkgdir}/opt/"
 
  # install launcher scripts
  install -Dm755 ${srcdir}/trine.sh ${pkgdir}/usr/bin/trine
  install -Dm755 ${srcdir}/trine-launcher.sh ${pkgdir}/usr/bin/trine-launcher
 
  # install desktop entry
  install -Dm644 ${srcdir}/trine.desktop ${pkgdir}/usr/share/applications/trine.desktop
 
  # binaries
  cd "${pkgdir}/opt/${pkgname}"
  if [[ "${CARCH}" == "x86_64" ]]; then
    rm -rf lib32 trine-bin32 trine-launcher32
    mv trine-bin{64,}
    mv trine-launcher{64,}
  elif [[ "${CARCH}" == "i686" ]]; then
    rm -rf lib64 trine-bin64 trine-launcher64
    mv trine-bin{32,}
    mv trine-launcher{32,}
  fi
 
  #fix file permissions
  chmod 644 "${pkgdir}/opt/${pkgname}/Trine_Manual_linux.pdf" "${pkgdir}/opt/${pkgname}/data/video/cinematic/trineroom_thief.ogg"
 
  #remove unneeded files
  cd "${pkgdir}/opt/${pkgname}/"
  rm {remove,create}Shortcuts.sh
 
  cd "${pkgdir}/opt/${pkgname}/lib"*
  #Need to keep the following non-system libraries:
  KEEP=(
    ## trine-bin
    #32bit
    'libavcodec.so.51'
    'libavformat.so.52'
    'libavutil.so.49'
    'libcv.so.1'
    'libcxcore.so.1'
    'libboost_thread-mt.so.1.35.0'
    'libSDL_image-1.2.so.0'
    'libjpeg.so.62'
    'libtiff.so.4'
    'libSDL_ttf-2.0.so.0'
    'libPhysXLoader.so.1'
    'libGLEW.so.1.5'
    'libCg.so'
    'libCgGL.so'
    #64bit
    'libavcodec.so.52'
    'libavformat.so.52'
    'libavutil.so.50'
    'libswscale.so.0'
    #'libcv.so.1' -> repeated
    #'libcxcore.so.1 -> repeated
    #'libboost_thread-mt.so.1.35.0' -> repeated
    #'libPhysXLoader.so.1 -> repeated
    #'libGLEW.so.1.5 -> repeated
 
    ## trine-launcher
    #32bit
    'libboost_regex.so.1.35.0'
    'libicudata.so.38'
    'libicui18n.so.38'
    'libicuuc.so.38'
    'libboost_filesystem.so.1.35.0'
    'libboost_system.so.1.35.0'
    #64bit
    #'libboost_regex.so.1.35.0' -> repeated
    #'libicudata.so.38' -> repeated
    #'libicui18n.so.38' -> repeated
    #'libicuuc.so.38' -> repeated
    #'libboost_filesystem.so.1.35.0' -> repeated
    #'libboost_system.so.1.35.0' -> repeated
 
    ## Game crashes without 'libPhysXCore.so' - Thanks to Dayita for finding this!
    'libPhysXCore.so'
    ## And also crashes without 'libNxCooking.so' - Thanks to PostBlue for finding this!
    'libNxCooking.so'
  )
  #Remove the rest
  for i in *; do
    if [[ ! "${KEEP[@]}" =~ "${i}" ]]; then
      rm "${i}"
    fi
  done
}
