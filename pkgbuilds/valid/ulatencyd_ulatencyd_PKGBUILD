# Maintainer: Inkane <neoinkaneglade@aol.com>
# Contributer: SpepS <dreamspepser at yahoo dot it>

pkgname=ulatencyd
pkgver=0.5.0
pkgrel=3
pkgdesc="Daemon to minimize latency on a linux system using cgroups"
arch=(i686 x86_64)
url="https://github.com/poelzi/ulatencyd"
license=('GPL3')
depends=('luaposix' 'dbus-glib' 'libxcb' 'polkit')
optdepends=('pyqt: qt gui'
            'systemd: systemd service')
makedepends=('cmake')
conflicts=("$pkgname-git")
backup=("etc/$pkgname/$pkgname.conf")
options=('!emptydirs')
source=("https://github.com/downloads/poelzi/$pkgname/$pkgname-$pkgver.tar.gz"
        "$pkgname.d" "$pkgname" "std.patch")
md5sums=('ef03b9c62ebc2f24327963e0c3a55eae'
         'ceb27033cb79d2a9ad71e390b0b75d7d'
         '2fb2440ceab34523d6207dfc53dc79d5'
         '6c3356fb18bd53dcb05987eb1a111d65')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # man path fix
  sed -i "s|/man/man|/share&|g" docs/CMakeLists.txt

  # closing STD* in daemon mode cause 100% CPU
  # quick fix: remove close(STD*_FILENO);
  # TODO: Signal this bug upstream
  # UPDATE: Applied official patch from poelzi
  patch -p1 -i ../std.patch

  cmake . -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # daemon
  install -Dm755 ../$pkgname "$pkgdir/etc/rc.d/$pkgname"
  install -Dm644 ../$pkgname.d "$pkgdir/etc/conf.d/$pkgname"

  # pyqt gui python2 fix
  sed -i 's/env python/&2/' "$pkgdir/usr/bin/ulatency"
}

# vim:set ts=2 sw=2 et:
