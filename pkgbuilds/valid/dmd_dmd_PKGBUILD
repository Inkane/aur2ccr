# $Id: PKGBUILD 64556 2012-02-16 07:01:07Z svenstaro $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Anders Bergh <anders1@gmail.com>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=dmd
pkgver=2.058
pkgrel=1
pkgdesc="The Digital Mars D compiler"
arch=('i686' 'x86_64')
url="http://www.digitalmars.com/d/2.0/"
source=(http://ftp.digitalmars.com/${pkgname}.$pkgver.zip
        tools.tar.gz::https://github.com/D-Programming-Language/tools/tarball/v${pkgver})
md5sums=('81a0fe7b635d3a38ecbefff6048a37fe'
         'cce5249b59a38a4c0eddf113aff87698')
license=('custom')

[[ $CARCH == "x86_64" ]] && _archbits="64"
[[ $CARCH == "i686" ]] && _archbits="32"

build() {
    cd $srcdir/${pkgname}2/src/

    cd ${pkgname}
    make -f posix.mak MODEL=$_archbits

    cd ../druntime
    make -f posix.mak MODEL=$_archbits DMD=../${pkgname}/${pkgname}

    cd ../phobos
    make -f posix.mak MODEL=$_archbits DMD=../${pkgname}/${pkgname}

    cd $srcdir/D-Programming-Language-tools-*
    $srcdir/${pkgname}2/src/${pkgname}/${pkgname} -I$srcdir/${pkgname}2/src/druntime/import/ -I$srcdir/${pkgname}2/src/phobos -L-L$srcdir/${pkgname}2/src/phobos/generated/linux/release/$_archbits/ r${pkgname}.d
}

package() {
  depends=('libphobos' 'gcc-libs')

  install -Dm755 $srcdir/${pkgname}2/src/${pkgname}/${pkgname} $pkgdir/usr/bin/${pkgname}

  mkdir -p $pkgdir/etc
  echo -e "[Environment]\nDFLAGS=-I/usr/include/d -I/usr/include/d/druntime/import -L-L/usr/lib -L-lrt" > $pkgdir/etc/${pkgname}.conf

  install -Dm644 $srcdir/${pkgname}2/man/man1/${pkgname}.1 $pkgdir/usr/share/man/man1/${pkgname}.1
  install -Dm644 $srcdir/${pkgname}2/man/man1/r${pkgname}.1 $pkgdir/usr/share/man/man1/r${pkgname}.1
  install -Dm644 $srcdir/${pkgname}2/man/man1/${pkgname}.conf.5 $pkgdir/usr/share/man/man5/${pkgname}.conf.5

  install -Dm644 $srcdir/${pkgname}2/license.txt $pkgdir/usr/share/licenses/${pkgname}/LICENSE

  mkdir -p $pkgdir/usr/share/d/samples/
  cp -r $srcdir/${pkgname}2/samples/d/* $pkgdir/usr/share/d/samples/

  install -Dm755 $srcdir/D-Programming-Language-tools-*/r${pkgname} $pkgdir/usr/bin/r${pkgname}
}

